;;;; abstract.cl
(in-package #:pycl)

;;; Conditions
(define-condition null-pyobject-error (simple-pycl-error)
  ()
  (:documentation "Prevent applying NULL pyobject(s) to Python C APIs."))

(define-condition pyobject-conversion-error (simple-pycl-error)
  ()
  (:documentation "Conversion errors between Lisp and Python."))

;;; Utilities
(defun check-null-pyobject (ob)
  (declare (type pyobject ob))
  (when (pynull ob)
    (error 'null-pyobject-error :msg "Illegal argument: NULL pyobject")))

(defun pyobject-conversion-error (&key from to)
  (error 'pyobject-conversion-error
         :msg (format nil "don't know to convert ~a to ~a" from to)))

(defmethod print-object ((ob pyobject) stream)
  (if* (= 0 (foreign-pointer-address ob))
     then (format stream "#<~a NULL>" (foreign-pointer-type ob))
     else (let ((*print-base* 16))
            (if* (pytype ob)
               then (format stream "#<~a (~a) @ #x~a>"
                                   (foreign-pointer-type ob)
                                   (pytype ob)
                                   (foreign-pointer-address ob))
               else (format stream "#<~a @ #x~a>"
                                   (foreign-pointer-type ob)
                                   (foreign-pointer-address ob))))))

;;; Abstract objects protocol
(defgeneric to-pyobject (thing)
  (:documentation "Default method for converting a lisp value to a pyobject instance.")
  (:method (thing) (pyobject-conversion-error :from thing :to 'pyobject))
  (:method ((ob pyobject)) ob))

(defgeneric pyhasattr (ob attr)
  (:documentation "Returns t if a pyobject `ob' has attribute `attr'; returns nil otherwise.")
  (:method :before ((ob pyobject) attr)
    (declare (ignore attr))
    (check-null-pyobject ob))
  (:method ((ob pyobject) attr)
    (with-native-string (str (string+ attr) :external-format :utf-8)
      (= 1 (PyObject_HasAttrString ob str)))))

(defgeneric pyattr (ob attr)
  (:documentation "Retrieve an attribute `attr' from pyobject ob. This is the equivalent of the
Python expression `ob.attr'.")
  (:method :before ((ob pyobject) attr)
    (declare (ignore attr))
    (check-null-pyobject ob))
  (:method ((ob pyobject) attr)
    (with-native-string (str (string+ attr) :external-format :utf-8)
      (values (pycheckn (PyObject_GetAttrString ob str))
              (= 1 (PyObject_HasAttrString ob str)))))
  (:method ((ob pyobject) (attr pyobject))
    (check-null-pyobject attr)
    (pycheckn (PyObject_GetAttr ob attr))))

(defmethod (setf pyattr) (new (ob pyobject) attr)
  (check-null-pyobject ob)
  (let ((ob-new (to-pyobject new)))
    (with-native-string (str (string+ attr) :external-format :utf-8)
      (if* (and (pynull ob-new) (= 1 (PyObject_HasAttrString ob str)))
         then (pycheckz (PyObject_DelAttrString ob str))
         else (pycheckz (PyObject_SetAttrString ob str ob-new))))))

(defmethod (setf pyattr) (new (ob pyobject) (attr pyobject))
  (check-null-pyobject ob)
  (check-null-pyobject attr)
  (let ((ob-new (to-pyobject new)))
    (if* (and (pynull ob-new) (= 1 (PyObject_HasAttrString ob attr)))
       then (pycheckz (PyObject_DelAttrString ob attr))
       else (pycheckz (PyObject_SetAttrString ob attr ob-new)))))

(defgeneric pyrepr (ob)
  (:documentation "Compute a string representation of pyobject `ob'. This is the equivalent of the
Python expression `repr(ob)'")
  (:method ((ob pyobject))
    (let ((ob-unicode (pycheckn (PyObject_Repr ob))))
      (unwind-protect (values (native-to-string (PyUnicode_AsUTF8 ob-unicode)
                                                :external-format :utf-8))
        (pydecref ob-unicode)))))

(defgeneric pystr (ob)
  (:documentation "Compute a string representation of pyobject `ob'. This is the equivalent of the
Python expression `str(ob)'")
  (:method ((ob pyobject))
    (let ((ob-unicode (pycheckn (PyObject_Str ob))))
      (unwind-protect (values (native-to-string (PyUnicode_AsUTF8 ob-unicode)
                                                :external-format :utf-8))
        (pydecref ob-unicode)))))

(defgeneric pybytes (ob)
  (:documentation "Compute a bytes representation of object `ob'.")
  (:method :before ((ob pyobject))
    (check-null-pyobject ob))
  (:method ((ob pyobject))
    (let ((ob_bytes (pycheckn (PyObject_Bytes ob))))
      (with-static-fobjects ((buffer '(* :char)  :allocation :c)
                             (size   'Py_ssize_t :allocation :c))
        (pycheckz (PyBytes_AsStringAndSize ob_bytes buffer size))
        (pydecref ob_bytes)
        (native-to-octets (fslot-value-typed '(* :char) :c buffer)
                          :length (fslot-value-typed 'Py_ssize_t :c size)
                          :null-terminate nil)))))

(defgeneric pyisinstance (ob type)
  (:documentation "Return t if `ob' is an instance of the class `type' or a subclass of `type', or
nil if not.")
  (:method :before ((ob pyobject) type)
    (declare (ignore type))
    (check-null-pyobject ob))
  (:method ((ob pyobject) (type pyobject))
    (check-null-pyobject type)
    (= (pycheckz (PyObject_IsInstance ob type))
       1))
  (:method ((ob pyobject) (type symbol))
    (= (multiple-value-bind (ptr exists-p) (pyglobalptr type)
         (if* exists-p
            then (pycheckz (PyObject_IsInstance ob ptr))
            else (error 'simple-pycl-error :msg (string+ "Don't know how to handle python type: " type))))
       1))
  (:method ((ob pyobject) (type integer))
    (check-type type (unsigned-byte #+32bit 32 #+64bit 64))
    (assert (not (zerop type)))
    (= (pycheckz (PyObject_IsInstance ob type))
       1)))

(defgeneric pyhash (ob)
  (:documentation "Compute and return the hash value of an pyobject `ob'")
  (:method :before ((ob pyobject))
    (check-null-pyobject ob))
  (:method ((ob pyobject))
    (pycheckz (PyObject_Hash ob))))

(defgeneric pyistrue (ob)
  (:documentation "Returns t if the pyobject `ob' is considered to be true, and nil otherwise.")
  (:method :before ((ob pyobject))
    (check-null-pyobject ob))
  (:method ((ob pyobject))
    (= (pycheckz (PyObject_IsTrue ob))
       1)))

(defgeneric pynot (ob)
  (:documentation "Returns t if the pyobject `ob' is considered to be true, and nil otherwise.")
  (:method :before ((ob pyobject))
    (check-null-pyobject ob))
  (:method ((ob pyobject))
    (= (pycheckz (PyObject_Not ob))
       1)))

(defgeneric pytype (ob)
  (:documentation "Returns a type symbol corresponding to the object type of pyobject `ob'")
  (:method ((ob pyobject))
    (if* (pynull ob)
       then (values nil *pynull*)
       else (let ((ob_type (fslot-value-typed 'PyObject
                                              :c
                                              (foreign-pointer-address ob)
                                              'ob_type)))
              (declare (type (unsigned-byte #+32bit 32 #+64bit 64) ob_type))
              (values  (pyglobalptr ob_type)
                       (make-instance 'pyobject :foreign-address ob_type
                                                :foreign-type 'PyTypeObject))))))

(defgeneric pyitem (ob key)
  (:documentation "Return element of `ob' corresponding to the object `key'.")
  (:method :before ((ob pyobject) key)
    (declare (ignore key))
    (check-null-pyobject ob))
  (:method ((ob pyobject) key)
    (setq key (to-pyobject key))
    (check-null-pyobject key)
    (pycheckn (PyObject_GetItem ob key))))

(defmethod (setf pyitem) (v (ob pyobject) key)
  (check-null-pyobject ob)
  (setq v (to-pyobject v)
        key (to-pyobject key))
  (check-null-pyobject key)
  (= (pycheckz (PyObject_SetItem ob key v))
     0))

(defgeneric pylen (ob)
  (:documentation "Return the length of pyobject `ob'. This is the equivalent to the Python
expression `len(ob)'.")
  (:method :before ((ob pyobject))
    (check-null-pyobject ob))
  (:method ((ob pyobject))
    (pycheckz (PyObject_Length ob))))

(defgeneric pydir (ob)
  (:documentation "This is equivalent to the Python expression `dir(ob)', returning a (possibly
empty) list of strings appropriate for the object argument.")
  (:method ((ob pyobject))
    (let ((ob-list (pycheckn (PyObject_Dir ob))))
      (unwind-protect (loop with lst = (list)
                            for i from 0 below (pylen ob-list)
                            for ob-unicode = (pycheckn (PySequence_GetItem ob-list i))
                            do (progn (push (native-to-string (PyUnicode_AsUTF8 ob-unicode)
                                                              :external-format :utf-8)
                                            lst)
                                      (pydecref ob-unicode))
                            finally (return (nreverse lst)))
        (pydecref ob-list)))))

(defgeneric pyiter (ob)
  (:documentation "This is equivalent to the Python expression `iter(ob)'.")
  (:method :before ((ob pyobject))
    (check-null-pyobject ob))
  (:method ((ob pyobject))
    (make-pyobject (pycheckn (PyObject_GetIter ob)))))

(defun pynumberp (ob)
  (and (typep ob 'pyobject)
       (= 1 (PyNumber_Check ob))))

(defun pysequencep (ob)
  (and (typep ob 'pyobject)
       (= 1 (PySequence_Check ob))))

(defun pymappingp (ob)
  (and (typep ob 'pyobject)
       (= 1 (PyMapping_Check ob))))

;;; Concrete objects protocol
(define-symbol-macro pynone
    (make-pyobject (pyglobalptr 'Py_None) :borrowed))

(defun make-pybool (x)
  (make-pyobject (pyglobalptr (if x 'Py_True 'Py_False))))

(defun make-pylong (x)
  (flet ((convert (x)
           (declare (type real x))
           (typecase x
             (unsigned-byte (pycheckn (PyLong_FromUnsignedLongLong x)))
             (integer (pycheckn (PyLong_FromLongLong x)))
             (t (pycheckn (PyLong_FromDouble (float x 0d0)))))))
    (if* (realp x)
       then (make-pyobject (convert x))
       else (pyobject-conversion-error :from x :to 'PyLong_Type))))

(defun make-pyfloat (x)
  (if* (realp x)
     then (make-pyobject (pycheckn (PyFloat_FromDouble (float (the real x) 0d0))))
     else (pyobject-conversion-error :from x :to 'PyFloat_Type)))

(defun make-pycomplex (x)
  (if* (complexp x)
     then (make-pyobject (pycheckn (PyComplex_FromDoubles (float (realpart x) 0d0)
                                                          (float (imagpart x) 0d0))))
     else (pyobject-conversion-error :from x :to 'PyComplex_Type)))

(defun make-pybytes (x)
  (flet ((convert (x)
           (with-native-string (str x :native-length-var len
                                      :external-format :utf-8)
             (make-pyobject (PyBytes_FromStringAndSize str len)))))
    (typecase x
      ((or simple-string (simple-array (unsigned-byte 8) (*)))
       (convert x))
      (list
       (convert (coerce x '(simple-array (unsigned-byte 8) (*)))))
      (t (pyobject-conversion-error :from x :to 'PyBytes_Type)))))

(defun make-pybytearray (x)
  (make-pyobject (typecase x
                   (pyobject
                    (pycheckn (PyByteArray_FromObject x)))
                   ((or simple-string
                        (simple-array (unsigned-byte 8) (*)))
                    (with-native-string (str x :native-length-var len :external-format :utf-8)
                      (pycheckn (PyByteArray_FromStringAndSize str len))))
                   (t
                    (pyobject-conversion-error :from x :to 'PyByteArray_Type)))))

(defun make-pyunicode (x)
  (typecase x
    ((or simple-string (simple-array (unsigned-byte 8) (*)))
     (with-native-string (cstr x :native-length-var len
                                 :external-format :utf-8)
       (make-pyobject (pycheckn (PyUnicode_FromStringAndSize cstr len)))))
    (t (pyobject-conversion-error :from x :to 'PyUnicode_Type))))

(defun make-pytuple (x)
  (flet ((convert (obs)
           (let ((ob_tuple (pycheckn (PyTuple_New (length obs)))))
             (loop for ob in obs
                   for idx from 0
                   do (PyTuple_SetItem ob_tuple idx (pystealref (to-pyobject ob)))
                   finally (return (make-pyobject ob_tuple))))))
    (typecase x
      (list (convert x))
      (array (loop with obs = (list)
                   for idx from 0 below (length x)
                   do (push (aref x idx) obs)
                   finally (return (convert (nreverse obs)))))
      (t (pyobject-conversion-error :from x :to 'PyTuple_Type)))))

(defun make-pylist (x)
  (flet ((convert (obs)
           (let ((ob_list (pycheckn (PyList_New (length obs)))))
             (loop for ob in obs
                   for idx from 0
                   do (PyList_SetItem ob_list idx (pystealref (to-pyobject ob)))
                   finally (return (make-pyobject ob_list))))))
    (typecase x
      (list (convert x))
      (array (loop with obs = (list)
                   for idx from 0 below (length x)
                   do (push (aref x idx) obs)
                   finally (return (convert (nreverse obs)))))
      (t (pyobject-conversion-error :from x :to 'PyList_Type)))))

(defun make-pydict (mapping)
  (let ((ob_dict (pycheckn (PyDict_New))))
    (typecase mapping
      (hash-table (with-hash-table-iterator (next mapping)
                    (while ()
                      (multiple-value-bind (more? k v) (next)
                        (if* more?
                           then (pycheckz (PyDict_SetItem ob_dict (to-pyobject k) (to-pyobject v)))
                           else (return (make-pyobject ob_dict)))))))
      (cons (loop for (k . v) in mapping
                  do (pycheckz (PyDict_SetItem ob_dict (to-pyobject k) (to-pyobject v)))
                  finally (return (make-pyobject ob_dict))))
      (t (pyobject-conversion-error :from mapping :to 'PyDict_Type)))))

(defmethod to-pyobject ((x (eql nil)))  pynone)
(defmethod to-pyobject ((x symbol))     (make-pyunicode (symbol-name x)))
(defmethod to-pyobject ((x integer))    (make-pylong x))
(defmethod to-pyobject ((x real))       (make-pyfloat x))
(defmethod to-pyobject ((x complex))    (make-pycomplex x))
(defmethod to-pyobject ((x list))       (make-pylist x))
(defmethod to-pyobject ((x array))      (make-pylist x))
(defmethod to-pyobject ((x string))     (make-pyunicode x))
(defmethod to-pyobject ((x hash-table)) (make-pydict x))

(defgeneric from-pyobject (ob ptype)
  (:documentation "Default method for converting a PyObject pointer to a pyobject instance.")
  (:method :before ((ob pyobject) ptype)
    (declare (ignore ptype))
    (check-null-pyobject ob))
  (:method ((ob pyobject) ptype)
    (declare (ignore ptype))
    (from-pyobject ob (pytype ob)))
  (:method ((ob pyobject) (ptype (eql 'PyBool_Type)))
    (= (foreign-pointer-address ob)
       (pyglobalptr 'Py_True)))
  (:method ((ob pyobject) (ptype (eql 'PyLong_Type)))
    (with-static-fobjects ((overflow :int :allocation :c))
      (let ((res (PyLong_AsLongLongAndOverflow ob overflow)))
        (case (fslot-value-typed :int :c overflow)
          (1  (error 'simple-pycl-error :msg (format "~a is greater than PY_LLONG_MAX" ob)))
          (-1 (error 'simple-pycl-error :msg (format "~a is less than PY_LLONG_MIN" ob)))
          (t res)))))
  (:method ((ob pyobject) (ptype (eql 'PyFloat_Type)))
    (let ((res (PyFloat_AsDouble ob)))
      (when python-exception-occurred
        (pyerror 'PyFloat_AsDouble))
      res))
  (:method ((ob pyobject) (ptype (eql 'PyComplex_Type)))
    (let ((r (PyComplex_RealAsDouble ob))
          (i (PyComplex_ImagAsDouble ob)))
      (complex r i)))
  (:method ((ob pyobject) (ptype (eql 'PyBytes_Type)))
    (with-static-fobjects ((buffer '(* :char) :allocation :c)
                           (len 'Py_ssize_t :allocation :c))
      (pycheckz (PyBytes_AsStringAndSize ob buffer len))
      (native-to-octets (fslot-value-typed '(* :char) :c buffer)
                        :length (fslot-value-typed 'Py_ssize_t :c len)
                        :null-terminate t)))
  (:method ((ob pyobject) (ptype (eql 'PyByteArray_Type)))
    (let ((ptr (pycheckn (PyByteArray_AsString ob))))
      (native-to-octets ptr :null-terminate t)))
  (:method ((ob pyobject) (ptype (eql 'PyUnicode_Type)))
    (values (native-to-string (PyUnicode_AsUTF8 ob) :external-format :utf-8)))
  (:method ((ob pyobject) (ptype (eql 'PyTuple_Type)))
    (loop with lst = (list)
          for i from 0 below (pylen ob)
          for ob = (pycheckn (PyTuple_GetItem ob i))
          do (push (from-pyobject (make-pyobject ob :borrowed) t) lst)
          finally (return (nreverse lst))))
  (:method ((ob pyobject) (ptype (eql 'PyList_Type)))
    (loop with lst = (list)
          for i from 0 below (pylen ob)
          for ob = (pycheckn (PyList_GetItem ob i))
          do (push (from-pyobject (make-pyobject ob :borrowed) t) lst)
          finally (return (nreverse lst)))))

;; (defun pyimport! (module)
;;   (declare (type simple-string module))
;;   (with-native-string (str module :external-format :utf-8)
;;     (the pyobject (PyImport_ImportModule str))))

;; (defun check-args/pysequence-getter-setter (ob-seq idx)
;;   (check-type ob-seq pyobject)
;;   (assert (= 1 (PySequence_Check ob-seq)))
;;   (check-type idx (unsigned-byte #+32bit 32 #+64bit 64))
;;   (assert (< idx (PyObject_Size ob-seq))))

;; (defun pysequence-get (ob-seq idx)
;;   (check-args/pysequence-getter-setter ob-seq idx)
;;   (pycheckn
;;    (cond ((pytypep ob-seq 'PyTuple_Type) (PyTuple_GetItem ob-seq idx))
;;          ((pytypep ob-seq 'PyList_Type) (PyList_GetItem ob-seq idx))
;;          (t (PySequence_GetItem ob-seq idx)))))

;; (defun pysequence-set (ob-seq idx new-val)
;;   (check-args/pysequence-getter-setter ob-seq idx)
;;   (setq new-val (to-pyobject new-val))
;;   (pycheckz
;;    (cond ((pytypep ob-seq 'PyTuple_Type) (PyTuple_SetItem ob-seq idx (pystealref new-val)))
;;          ((pytypep ob-seq 'PyList_Type) (PyList_SetItem ob-seq idx (pystealref new-val)))
;;          (t (if* (pynull new-val)
;;                then (PySequence_DelItem ob-seq idx)
;;                else (PySequence_SetItem ob-seq idx new-val))))))

;; (defun pyhaskey (ob key)
;;   (check-type ob pyobject)
;;   (check-type key string)
;;   (assert (= 1 (PyMapping_Check ob)))
;;   (with-native-string (str key :external-format :utf-8)
;;     (= 1 (PyMapping_HasKeyString ob str))))

;; (defun pymapping-get (ob key)
;;   (check-type ob pyobject)
;;   (check-type key string)
;;   (assert (= 1 (PyMapping_Check ob)))
;;   (with-native-string (str key :external-format :utf-8)
;;     (values (pycheckn (PyMapping_GetItemString ob str))
;;             (= 1 (PyMapping_HasKeyString ob str)))))

;; (defun pymapping-set (ob key new-val)
;;   (check-type ob pyobject)
;;   (check-type key string)
;;   (assert (= 1 (PyMapping_Check ob)))
;;   (setq new-val (to-pyobject new-val))
;;   (with-native-string (str key :external-format :utf-8)
;;     (= 0 (pycheckz (PyMapping_SetItemString ob str new-val)))))
